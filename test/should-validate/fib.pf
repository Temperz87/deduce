import Nat
import Sums

recfun fib(n : Nat) -> Nat
  measure n of Nat
{
  if n < ℕ2 then n
  else fib(n ∸ ℕ1) + fib(n ∸ ℕ2)
}
terminates {
  arbitrary n:Nat
  have A: if not (n < ℕ2) then n ∸ ℕ1 < n by {
    assume prem: not (n < ℕ2)
    have n1: ℕ1 ≤ n by {
      have n2: ℕ2 ≤ n by apply not_less_implies_less_equal to prem
      have lt_12: ℕ1 ≤ ℕ2 by evaluate
      apply less_equal_trans to lt_12, n2
    }
    suffices ℕ1 + (n ∸ ℕ1) < ℕ1 + n  by add_both_sides_of_less[ℕ1,n∸ℕ1,n]
    suffices n < ℕ1 + n by replace (apply monus_add_identity[n, ℕ1] to n1).
    suffices n ≤ n by evaluate
    less_equal_refl
  }
  have B: if not (n < ℕ2) then n ∸ ℕ2 < n by {
    assume prem: not (n < ℕ2)
    suffices ℕ1 + (n ∸ ℕ2) ≤ n by expand operator< replace nat_suc_one_add .
    have n2: ℕ2 ≤ n by apply not_less_implies_less_equal to prem
    have n_eq: n = ℕ2 + (n ∸ ℕ2) by symmetric apply monus_add_identity[n, ℕ2] to n2
    define a = n ∸ ℕ2
    replace n_eq
    show ℕ1 + a ≤ ℕ2 + a
    suffices a ≤ ℕ1 + a by add_both_sides_of_less_equal[ℕ1, a, ℕ1 + a]
    less_equal_add_left
  }
  A, B
}

assert fib(ℕ0) = ℕ0
assert fib(ℕ1) = ℕ1
assert fib(ℕ2) = ℕ1
assert fib(ℕ3) = ℕ2
assert fib(ℕ4) = ℕ3
assert fib(ℕ5) = ℕ5
assert fib(ℕ6) = ℕ8

/*
theorem add_suc_left: all a:Nat, b:Nat. a + suc(b) = suc(a) + b
proof
  arbitrary a:Nat, b:Nat
  equations
      a + suc(b) = suc(a + b)   by add_suc
             ... = #suc(a) + b# by expand operator+.
end
*/

theorem fib_2_n: all n:Nat. fib(ℕ2 + n) = fib(ℕ1 + n) + fib(n)
proof
  arbitrary n:Nat
  switch n {
    case zero {
      conclude fib(ℕ2 + ℕ0) = fib(ℕ1 + ℕ0) + fib(zero)   by evaluate
    }
    case suc(n') {
      replace nat_suc_one_add
      switch n' {
        case 0 {
          conclude fib(ℕ2 + ℕ1) = fib(ℕ1 + ℕ1) + fib(ℕ1)  by evaluate
        }
        case suc(n'') {
          have A: (ℕ4 + n'' < ℕ2) = false by evaluate
          have B: (ℕ3 + n'' < ℕ2) = false by evaluate
          have C: (ℕ2 + n'' < ℕ2) = false by evaluate
          expand fib
          replace A | B | C
          evaluate
        }
      }
    }
  }
end

theorem fib_1_pos: all n:Nat. ℕ0 < fib(ℕ1 + n)
proof
  induction Nat
  case 0 {
    conclude ℕ0 < fib(ℕ1 + ℕ0)  by evaluate
  }
  case suc(n') assume IH: ℕ0 < fib(ℕ1 + n') {
    replace nat_suc_one_add
    have A: (ℕ2 + n' < ℕ2) = false by evaluate
    expand fib
    replace A
    replace add_monus_identity[ℕ2] 
    replace add_monus_identity[ℕ1, ℕ1 + n'] 
    suffices ℕ1 ≤ fib(ℕ1 + n') + fib(n')
      by expand operator<.
    have B: ℕ1 ≤ fib(ℕ1 + n')  by expand operator< in IH
    have C: fib(ℕ1 + n') ≤ fib(ℕ1 + n') + fib(n') by less_equal_add
    conclude ℕ1 ≤ fib(ℕ1 + n') + fib(n')
      by apply less_equal_trans to B, C
  }
end

theorem sum_fib: all n:Nat. summation(suc(n), ℕ1, fib) = fib(ℕ3 + n) ∸ ℕ1
proof
  induction Nat
  case 0 {
    suffices fib(ℕ1) = fib(ℕ3) ∸ ℕ1 by expand 2*summation.
    evaluate
  }
  case suc(n') assume IH: summation(suc(n'), ℕ1, fib) = fib(ℕ3 + n') ∸ ℕ1 {
    replace nat_suc_one_add
    have IH': summation(ℕ1 + n', ℕ1, fib) = fib(ℕ3 + n') ∸ ℕ1
      by replace nat_suc_one_add in IH    
    suffices (fib(ℕ3 + n') ∸ ℕ1) + fib(ℕ2 + n') = fib(ℕ4 + n') ∸ ℕ1
      by replace summation_next[ℕ1 + n', ℕ1, fib] | IH'.
    replace fib_2_n[ℕ1+n'] | fib_2_n[ℕ2+n']
    replace fib_2_n[ℕ1+n'] | fib_2_n[n']
    define X = fib(n')
    define Y = fib(ℕ1 + n')
    have one_YXY: ℕ1 ≤ Y + X + Y by {
      expand Y
      have f_pos: ℕ0 < fib(ℕ1 + n') by fib_1_pos
      obtain Z where eq: fib(ℕ1 + n') = ℕ1 + Z
        from apply nat_positive_suc[fib(ℕ1 + n')] to f_pos
      replace eq
      evaluate
    }
    equations
            ((Y + X + Y) ∸ ℕ1) + Y + X
          = Y + X + ((Y + X + Y) ∸ ℕ1)  by replace add_commute[((Y+X+Y) ∸ ℕ1)].
      ... = (Y + X + Y + X + Y) ∸ ℕ1
                                by apply monus_add_assoc[Y+X+Y, Y+X, ℕ1] to one_YXY
      ... = (Y + X + Y + #Y + X#) ∸ ℕ1  by replace add_commute.
  }
end
  
